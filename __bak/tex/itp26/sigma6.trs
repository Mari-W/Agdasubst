(VAR x e e1 e2 k t t1 t2 r r1 r2 r3 s s1 s2 s3 h)
(RULES
  liftR(r) -> consR(zero, compRR(r, wk))
  liftS(s) -> consS(var(zero), compSR(s, wk))

  appR(zero, consR(h, r)) -> h
  appR(suc(x), consR(h, r)) -> appR(x, r)
  
  appR(x, wk) -> suc(x)
  appR(x, compRR(r1, r2)) -> appR(appR(x, r1), r2)
  appR(x, idR) -> x
  
  appS(zero, consS(h, s)) -> h
  appS(suc(x), consS(h, s)) -> appS(x, s)
  
  appS(x, compSS(s1, s2)) -> sub(appS(x, s1), s2)
  appS(x, compRS(r, s)) -> appS(appR(x, r), s)
  appS(x, compSR(s, r)) -> ren(appS(x, s), r)
  appS(x, idS) -> var(x)

  compRR(consR(h, r1), r2) -> consR(appR(h, r2), compRR(r1, r2))
  compSR(consS(h, s), r) -> consS(ren(h, r), compSR(s, r))
  
  compRS(consR(h, r), s) -> consS(appS(h, s), compRS(r, s))
  compSS(consS(h, s1), s2) -> consS(sub(h, s2), compSS(s1, s2))

  compRR(r, idR) -> r
  compRR(idR, r) -> r
  compRR(compRR(r1, r2), r3) -> compRR(r1, compRR(r2, r3))
  
  compSS(s, idS) -> s
  compSS(idS, s) -> s
  compSR(s, idR) -> s
  compRS(idR, s) -> s

  compRS(compRR(r1, r2), s3) -> compRS(r1, compRS(r2, s3))
  compSR(compRS(r1, s2), r3) -> compRS(r1, compSR(s2, r3))
  compSS(compRS(r1, s2), s3) -> compRS(r1, compSS(s2, s3))
  compSR(compSR(s1, r2), r3) -> compSR(s1, compRR(r2, r3))
  compSS(compSR(s1, r2), s3) -> compSS(s1, compRS(r2, s3))
  compSR(compSS(s1, s2), r3) -> compSS(s1, compSR(s2, r3))
  compSS(compSS(s1, s2), s3) -> compSS(s1, compSS(s2, s3))

  ren(var(x), r) -> var(appR(x, r))
  ren(lam(e), r) -> lam(ren(e, liftR(r)))
  ren(Lam(e), r) -> Lam(ren(e, liftR(r)))
  ren(Pi(k, t), r) -> Pi(ren(k, r), ren(t, liftR(r)))
  ren(app(e1, e2), r) -> app(ren(e1, r), ren(e2, r))
  ren(tapp(e, t), r) -> tapp(ren(e, r), ren(t, r))
  ren(arr(t1, t2), r) -> arr(ren(t1, r), ren(t2, r))
  ren(star, r) -> star

  sub(var(x), s) -> appS(x, s)
  sub(lam(e), s) -> lam(sub(e, liftS(s)))
  sub(Lam(e), s) -> Lam(sub(e, liftS(s)))
  sub(Pi(k, t), s) -> Pi(sub(k, s), sub(t, liftS(s)))
  sub(app(e1, e2), s) -> app(sub(e1, s), sub(e2, s))
  sub(tapp(e, t), s) -> tapp(sub(e, s), sub(t, s))
  sub(arr(t1, t2), s) -> arr(sub(t1, s), sub(t2, s))
  sub(star, s) -> star


  ren(t, idR) -> t
  ren(ren(t, r1), r2) -> ren(t, compRR(r1, r2))
  
  sub(t, idS) -> t
  sub(ren(t, r), s) -> sub(t, compRS(r, s))
  ren(sub(t, s), r) -> sub(t, compSR(s, r))
  sub(sub(t, s1), s2) -> sub(t, compSS(s1, s2))

  compRR(wk, consR(h, r)) -> r
  compRS(wk, consS(h, s)) -> s

  compRS(r, idS) -> compSR(idS, r)
  compSS(s, compSR(idS, r)) -> compSR(s, r)
  compSS(compSR(idS, r), s) -> compRS(r, s)
  compRS(r1, compSR(idS, r2)) -> compSR(idS, compRR(r1, r2))
  sub(t, compSR(idS, r)) -> ren(t, r)
)